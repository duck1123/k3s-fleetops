{
  config,
  lib,
  self,
  ...
}:
with lib;
let
  secret-encryption-key-secret = "homarr-secret-encryption-key";
in
self.lib.mkArgoApp { inherit config lib; } rec {
  name = "homarr";
  uses-ingress = true;

  sopsSecrets = cfg:
    lib.optionalAttrs (cfg.secretEncryptionKey != "") {
      ${secret-encryption-key-secret} = {
        SECRET_ENCRYPTION_KEY = cfg.secretEncryptionKey;
      };
    };

  extraOptions = {
    image = mkOption {
      description = mdDoc "The Homarr docker image";
      type = types.str;
      default = "ghcr.io/homarr-labs/homarr:latest";
    };

    service.port = mkOption {
      description = mdDoc "The service port";
      type = types.int;
      default = 7575;
    };

    storageClassName = mkOption {
      description = mdDoc "The storage class for appdata";
      type = types.str;
      default = "longhorn";
    };

    secretEncryptionKey = mkOption {
      description = mdDoc "64-character hex string for encryption (generate with: openssl rand -hex 32). Stored in SOPS secret.";
      type = types.str;
      default = "";
    };

    tz = mkOption {
      description = mdDoc "The timezone";
      type = types.str;
      default = "Etc/UTC";
    };

    defaultColorScheme = mkOption {
      description = mdDoc "Default theme: dark or light";
      type = types.str;
      default = "dark";
    };

    disableAnalytics = mkOption {
      description = mdDoc "Disable analytics";
      type = types.bool;
      default = true;
    };

    replicas = mkOption {
      description = mdDoc "Number of replicas";
      type = types.int;
      default = 1;
    };
  };

  extraResources = cfg: {
    deployments = {
      ${name} = {
        metadata.labels = {
          "app.kubernetes.io/instance" = name;
          "app.kubernetes.io/name" = name;
          "app.kubernetes.io/version" = "latest";
        };

        spec = {
          replicas = cfg.replicas;
          selector.matchLabels = {
            "app.kubernetes.io/instance" = name;
            "app.kubernetes.io/name" = name;
          };

          template = {
            metadata.labels = {
              "app.kubernetes.io/instance" = name;
              "app.kubernetes.io/name" = name;
            };

            spec = {
              automountServiceAccountToken = true;
              serviceAccountName = "default";

              containers = [
                {
                  inherit name;
                  image = cfg.image;
                  imagePullPolicy = "IfNotPresent";
                  env = [
                    {
                      name = "TZ";
                      value = cfg.tz;
                    }
                    {
                      name = "DEFAULT_COLOR_SCHEME";
                      value = cfg.defaultColorScheme;
                    }
                    {
                      name = "DISABLE_ANALYTICS";
                      value = if cfg.disableAnalytics then "true" else "false";
                    }
                  ]
                  ++ (
                    if cfg.secretEncryptionKey != "" then
                      [
                        {
                          name = "SECRET_ENCRYPTION_KEY";
                          valueFrom = {
                            secretKeyRef = {
                              name = secret-encryption-key-secret;
                              key = "SECRET_ENCRYPTION_KEY";
                            };
                          };
                        }
                      ]
                    else
                      [ ]
                  );
                  ports = [
                    {
                      containerPort = cfg.service.port;
                      name = "http";
                      protocol = "TCP";
                    }
                  ];
                  readinessProbe = {
                    httpGet = {
                      path = "/";
                      port = cfg.service.port;
                    };
                    initialDelaySeconds = 15;
                    periodSeconds = 10;
                    timeoutSeconds = 5;
                    successThreshold = 1;
                    failureThreshold = 3;
                  };
                  livenessProbe = {
                    httpGet = {
                      path = "/";
                      port = cfg.service.port;
                    };
                    initialDelaySeconds = 30;
                    periodSeconds = 30;
                    timeoutSeconds = 5;
                    successThreshold = 1;
                    failureThreshold = 3;
                  };
                  volumeMounts = [
                    {
                      mountPath = "/appdata";
                      name = "appdata";
                    }
                  ];
                }
              ];

              volumes = [
                {
                  name = "appdata";
                  persistentVolumeClaim.claimName = "${name}-${name}-appdata";
                }
              ];
            };
          };
        };
      };
    };

    ingresses.${name}.spec = with cfg.ingress; {
      inherit ingressClassName;

      rules = [
        {
          host = domain;

          http.paths = [
            {
              backend.service = {
                inherit name;
                port.name = "http";
              };

              path = "/";
              pathType = "ImplementationSpecific";
            }
          ];
        }
      ];

      tls = [ { hosts = [ domain ]; } ];
    };

    persistentVolumeClaims = {
      "${name}-${name}-appdata".spec = {
        accessModes = [ "ReadWriteOnce" ];
        resources.requests.storage = "1Gi";
        storageClassName = cfg.storageClassName;
      };
    };

    services.${name}.spec = {
      ports = [
        {
          name = "http";
          port = cfg.service.port;
          protocol = "TCP";
          targetPort = "http";
        }
      ];

      selector = {
        "app.kubernetes.io/instance" = name;
        "app.kubernetes.io/name" = name;
      };

      type = "ClusterIP";
    };
  };
}
